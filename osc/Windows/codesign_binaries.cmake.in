# This is a CPack pre-build script that signs all of the binaries in an install directory
# before they're packaged into the final installer (which must also be signed, but in
# a different script).

# Find `.exe`/`.dll`s that need to be signed
file(GLOB_RECURSE OSC_PACKAGE_EXES "${CPACK_TEMPORARY_INSTALL_DIRECTORY}/*.exe")
file(GLOB_RECURSE OSC_PACKAGE_DLLS "${CPACK_TEMPORARY_INSTALL_DIRECTORY}/*.dll")

# Sign each binary
foreach(OSC_BINARY_FILE IN LISTS OSC_PACKAGE_EXES OSC_PACKAGE_DLLS)
    message(STATUS "Running signtool on ${OSC_BINARY_FILE}")
    execute_process(
        COMMAND
            signtool sign
            /n  "@OSC_CERTIFICATE_SUBJECT@"
            /tr "@OSC_CERTIFICATE_TIMESTAMP_SERVER@"
            /td "@OSC_CERTIFICATE_TIMESTAMP_DIGEST@"
            /fd "@OSC_CODESIGN_DIGEST_ALGORITHM@"
            /v
            "${OSC_BINARY_FILE}"
        RESULT_VARIABLE res
        COMMAND_ECHO STDERR
    )
    if(NOT res EQUAL 0)
        message(FATAL_ERROR "signtool sign failed for file ${OSC_BINARY_FILE}")
    endif()
endforeach()

# Verify that every binary has been signed
foreach(OSC_SIGNED_BINARY_FILE IN LISTS OSC_PACKAGE_EXES OSC_PACKAGE_DLLS)
    message(STATUS "Verifying signature of ${OSC_SIGNED_BINARY_FILE}")
    execute_process(
        COMMAND signtool verify /pa /v "${OSC_SIGNED_BINARY_FILE}"
        RESULT_VARIABLE verify_res
        COMMAND_ECHO STDERR
    )
    if(NOT verify_res EQUAL 0)
        message(FATAL_ERROR "Signature verification failed for ${OSC_SIGNED_BINARY_FILE}")
    endif()
endforeach()
